
//Возвращает ссылку на текущего пользователя базы данных
Функция ТекущийПользователь()Экспорт
	
	Возврат ПараметрыСеанса.ТекущийПользователь;
	
КонецФункции

// Функция возвращает ссылку на элемент справочника "Пользователи",
// соответствующий текущему пользователю информационной базы.
Функция ОпределитьТекущегоПользователя() экспорт
	
	Если ПустаяСтрока(ИмяПользователя())
		или НРег(ИмяПользователя()) = "admin_sys" 
		или НРег(ИмяПользователя()) = "externalservice" Тогда
		
		ИмяПользователя           = "НеАвторизован";
		ПолноеИмяПользователя     = "Не авторизован"; 
		
	Иначе
		
		ИмяПользователя = ИмяПользователя();
		
		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			
			ПолноеИмяПользователя = ИмяПользователя;
			
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДлинаКодаПользователя = Метаданные.Справочники.Пользователи.ДлинаКода;
	
	Если СтрДлина(ИмяПользователя) > ДлинаКодаПользователя Тогда
		
		ИмяПользователя = Лев(ИмяПользователя, ДлинаКодаПользователя);
		
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ИмяПользователя);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		ОбъектПользователь = Справочники.Пользователи.СоздатьЭлемент();
		
		ОбъектПользователь.Код          = ИмяПользователя;
		ОбъектПользователь.Наименование = ПолноеИмяПользователя;
		
		Попытка
			ОбъектПользователь.Записать();
			
		Исключение
			
			ВызватьИсключение "Пользователь : " + ИмяПользователя + " не был найден в справочнике пользователей. Возникла ошибка при добавлении пользователя в справочник.
			|" + ОписаниеОшибки();
			
			Возврат Справочники.Пользователи.ПустаяСсылка();
			
		КонецПопытки;
		
		ТекущийПользователь = ОбъектПользователь.Ссылка;
		
	Иначе
		
		Выборка				= Результат.Выбрать();
		Выборка.Следующий();
		ТекущийПользователь	= Выборка.Ссылка;
		
	КонецЕсли; 
	
	Возврат ТекущийПользователь;
	
КонецФункции 

Функция СменитьПарольТекущегоПользователя(НовыйПароль,стрОписаниеОшибки = "")Экспорт 
	
	Попытка
		
		НачатьТранзакцию();	
		спрПользователь			= ТекущийПользователь().ПолучитьОбъект();
		пПользователь			= ПользователиИнформационнойБазы.НайтиПоИмени(спрПользователь.Код);
		пПользователь.Пароль	= НовыйПароль;
		спрПользователь.Пароль	= НовыйПароль;
		спрПользователь.ТребоватьСменуПароляПриВходе = Ложь;
		пПользователь.Записать();
		спрПользователь.Записать();
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			
			ОтменитьТранзакцию();
			
		КонецЕсли; 
		
		стрОписаниеОшибки = ИнформацияОбОшибке().Описание;
		
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции
 
Процедура УстановитьПараметр(Имя,Значение)Экспорт
	
	Попытка
		
		спрПользователь			= ТекущийПользователь().ПолучитьОбъект();
		спрПользователь[Имя]	= Значение;
		спрПользователь.Записать();
		
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры
 
Функция ТребоватьСменуПароляПриВходе()Экспорт
	
	Возврат ТекущийПользователь().ТребоватьСменуПароляПриВходе;	
	
КонецФункции
 
