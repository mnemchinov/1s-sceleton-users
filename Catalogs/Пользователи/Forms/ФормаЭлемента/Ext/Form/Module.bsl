&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		пПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(Объект.Код);
		ПользовательИБДоИзменения = ?(Объект.ПометкаУдаления, "", Объект.Код);
		
	Иначе
		
		пПользователь = Неопределено;
	КонецЕсли;
	
	Для каждого ТекРоль Из Метаданные.Роли Цикл
		
		Если Найти(ТекРоль.Имя,"ExternalService")>0 Тогда 
			
			Продолжить 
			
		КонецЕсли;
		
		ДоступныеРолиРоль		= ДоступныеРоли.Добавить();
		ДоступныеРолиРоль.Роль	= ТекРоль;
		
		Если пПользователь <> Неопределено и пПользователь.Роли.Содержит(ТекРоль) Тогда 
			
			ДоступныеРолиРоль.Пометка = Истина 
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ПроверитьЗаполнение() Тогда
		
		Отказ = Истина;
		
		Возврат;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ПользовательИБДоИзменения) Тогда
		
		пПользователь = ПользователиИнформационнойБазы.СоздатьПользователя();
		
	Иначе
		
		пПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(ПользовательИБДоИзменения);
		
	КонецЕсли;
	
	Если ТекущийОбъект.ПометкаУдаления и не ПустаяСтрока(ПользовательИБДоИзменения) Тогда
		
		пПользователь.Удалить();
		ПользовательИБДоИзменения = "";
		
	Иначе	
		
		пПользователь.Имя                       = ТекущийОбъект.Код;
		пПользователь.ПолноеИмя                 = ТекущийОбъект.Наименование;
		пПользователь.АутентификацияСтандартная = Истина;
		пПользователь.Роли.Очистить();
		
		Для каждого ТекРоль Из Метаданные.Роли Цикл
			
			Если Найти(ТекРоль.Имя,"ExternalService")>0 Тогда 
				
				Продолжить 
				
			КонецЕсли;
			
			спРоль = ДоступныеРоли.НайтиСтроки(Новый Структура("Роль",Строка(ТекРоль)));
			спРоль = спРоль[0];
			
			Если спРоль.Пометка Тогда
				
				пПользователь.Роли.Добавить(ТекРоль);
				
			КонецЕсли;
			
		КонецЦикла;
		
		пПользователь.ПоказыватьВСпискеВыбора   = ТекущийОбъект.ПоказыватьВСпискеВыбора;
		пПользователь.Язык                      = Метаданные.Языки.Русский;
		
		Если ТекущийОбъект.Пароль <> "********" Тогда
			
			пПользователь.Пароль                    = ТекущийОбъект.Пароль;
			
		КонецЕсли;
		
		пПользователь.Записать();
		ПользовательИБДоИзменения = пПользователь.Имя;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Объект.Пароль = "********";
	
КонецПроцедуры

